plugins {
    id 'java'
    id 'checkstyle'
	id 'maven-publish'
    id "com.diffplug.spotless" version "6.18.0"
}

sourceCompatibility = 17
targetCompatibility = 17

group 'net.fabricmc'
archivesBaseName = 'name-proposal'

def ENV = System.getenv()
version '0.2.0' + (ENV.GITHUB_ACTIONS ? "" : "+local")

repositories {
    maven {
        name = 'Fabric'
        url = 'https://maven.fabricmc.net/'
    }
    mavenCentral()
}

dependencies {
    implementation 'org.ow2.asm:asm:9.5'
    implementation 'org.ow2.asm:asm-commons:9.5'
    implementation 'org.ow2.asm:asm-tree:9.5'
    implementation 'org.ow2.asm:asm-util:9.5'

    implementation 'net.fabricmc:mapping-io:0.5.1'

    compileOnly 'cuchaz:enigma:2.3.1'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.3'
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 17
}

java {
    withSourcesJar()
}

test {
    useJUnitPlatform()
}

spotless {
    encoding 'UTF-8'
    lineEndings 'UNIX'
    java {
        licenseHeaderFile(rootProject.file("HEADER")).yearSeparator(", ")
    }
}

checkstyle {
    configFile = project.file("checkstyle.xml")
    toolVersion = "10.11.0"
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	repositories {
		mavenLocal()

		if (ENV.MAVEN_URL) {
			repositories.maven {
				name "fabric"
				url ENV.MAVEN_URL
				credentials {
					username ENV.MAVEN_USERNAME
					password ENV.MAVEN_PASSWORD
				}
			}
		}
	}
}
